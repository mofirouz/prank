apply plugin: 'idea'
apply plugin: 'java'
version = '0.0.1-SNAPSHOT'
group = 'com.mofirouz.prank'

idea.project.ipr {
    withXml { provider ->
        provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
    }
}

plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    tasks.withType(Compile).all {
        options.compilerArgs << "-Xlint:{all}" << "-Werror"
        options.encoding = "UTF-8"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        compile(
            [group: 'jcifs', name: 'jcifs', version: '1.3.17'],
            [group: 'com.google.guava', name: 'guava', version: '14.0.1']
        )
    }

    jar {
        dependsOn configurations.runtime

        manifest {
            attributes "Implementation-Title": project.name,
                       "Implementation-Version": project.version,
                       "Created-By": "Gradle " + gradle.gradleVersion,
                       "Build-Jdk": System.properties['java.version'],
                       "Manifest-Version": 1.0,
                       'Main-Class': 'com.mofirouz.prank.Main'
        }
        from(configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }) {
            // remove all signature files
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
    }
}